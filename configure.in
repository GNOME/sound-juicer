# Initial blurb
AC_INIT(sound-juicer, 2.15.5.1, http://www.burtonini.com/)
AC_PREREQ(2.52)
AM_INIT_AUTOMAKE([check-news 1.6])
AC_CONFIG_MACRO_DIR([m4])
AC_CONFIG_SRCDIR(libjuicer/sj-extractor.c)
AM_CONFIG_HEADER(config.h)
AM_MAINTAINER_MODE

# Honour user-set aclocal flags
ACLOCAL="$ACLOCAL $ACLOCAL_FLAGS"


# Do the gettext/i18n stuff
GETTEXT_PACKAGE=sound-juicer
AC_DEFINE_UNQUOTED(GETTEXT_PACKAGE, "$GETTEXT_PACKAGE", [The name of the gettext package.])
AC_SUBST(GETTEXT_PACKAGE)
AM_GLIB_GNU_GETTEXT


# Standard C and C++ compiler mojo
AC_ISC_POSIX
AC_PROG_CC
AM_PROG_CC_STDC
AC_HEADER_STDC
AC_PROG_CXX
AC_PROG_LIBTOOL
IT_PROG_INTLTOOL([0.35.0])

GNOME_COMPILE_WARNINGS

# Use the GNOME documentation framework
GNOME_DOC_INIT

# Find GLib and GObject
PKG_CHECK_MODULES(GLIB, glib-2.0 gthread-2.0 gobject-2.0)
AC_SUBST(GLIB_CFLAGS)
AC_SUBST(GLIB_LIBS)

# Find nautilus-burn
PKG_CHECK_MODULES(BURN, libnautilus-burn >= 2.15.3)
AC_SUBST(BURN_CFLAGS)
AC_SUBST(BURN_LIBS)

# Find the UI libraries
PKG_CHECK_MODULES(UI, gtk+-2.0 >= 2.8  libglade-2.0  gconf-2.0  libgnomeui-2.0 >= 2.13.0  gnome-vfs-2.0 >= 2.9 gnome-vfs-module-2.0 > 2.9 gmodule-2.0)
AC_SUBST(UI_CFLAGS)
AC_SUBST(UI_LIBS)

PKG_CHECK_MODULES(GSTREAMER, gstreamer-0.10 >= 0.10.3 gstreamer-plugins-base-0.10)
AC_SUBST(GSTREAMER_CFLAGS)
AC_SUBST(GSTREAMER_LIBS)

# Find the GStreamer elements we use, and warn if they are not present.
AM_GST_ELEMENT_CHECK(cdparanoiasrc,,AC_MSG_WARN([The 'cdparanoia' element was not found. This will cause Sound Juicer to fail at runtime.]))
AM_GST_ELEMENT_CHECK(vorbisenc,,AC_MSG_WARN([The 'vorbisenc' element was not found. This will cause encoding to Ogg Vorbis to fail.]))
AM_GST_ELEMENT_CHECK(flacenc,,AC_MSG_WARN([The 'flacenc' element was not found. This will cause encoding to FLAC to fail.]))
AM_GST_ELEMENT_CHECK(wavenc,,AC_MSG_WARN([The 'wavenc' element was not found. This will cause encoding to Wave to fail.]))
AM_GST_ELEMENT_CHECK(gnomevfssink,,AC_MSG_WARN([The 'gnomevfssink' element was not found. This will cause Sound Juicer to fail at runtime.]))

# Find GNOME Media Profiles
PKG_CHECK_MODULES(MEDIA_PROFILES, gnome-media-profiles >= 2.11.91)
AC_SUBST(MEDIA_PROFILES_CFLAGS)
AC_SUBST(MEDIA_PROFILES_LIBS)

# Find MusicBrainz
PKG_CHECK_MODULES(MUSICBRAINZ, libmusicbrainz >= 2.1.0)
AC_SUBST(MUSICBRAINZ_CFLAGS)
AC_SUBST(MUSICBRAINZ_LIBS)
oldlibs=$LIBS ; LIBS=$MUSICBRAINZ_LIBS
AC_CHECK_FUNCS(mb_SetProxyCreds)
LIBS=$oldlibs

# Find the optional dependency libcdio
PKG_CHECK_MODULES(CDIO, libcdio >= 0.70, HAVE_CDIO=yes, HAVE_CDIO=no)
if test "x$HAVE_CDIO" = "xyes"; then
        AM_CONDITIONAL(HAVE_CDIO, true)
else
        AM_CONDITIONAL(HAVE_CDIO, false)
        AC_MSG_WARN([libcdio not found, cannot get track names from CD-TEXT])
fi

# Find TagLib for the internal copy of id3mux
PKG_CHECK_MODULES(TAGLIB, taglib, HAVE_TAGLIB=yes, HAVE_TAGLIB=no)
if test "x$HAVE_TAGLIB" = "xyes"; then
        AM_CONDITIONAL(HAVE_TAGLIB, true)
        AC_SUBST(TAGLIB_CFLAGS)
        AC_SUBST(TAGLIB_LIBS)
else
        AM_CONDITIONAL(HAVE_TAGLIB, false)
        AC_MSG_WARN([taglib not found, cannot handle missing id3mux])
fi

# Find how and where to put the GConf schemas
AC_PATH_PROG(GCONFTOOL, gconftool-2, no)
if test x"$GCONFTOOL" = xno; then
        AC_MSG_ERROR([gconftool-2 executable not found in your path - should be installed with GConf])
fi
AM_GCONF_SOURCE_2


# Optionally enable G* deprecations
AC_MSG_CHECKING([whether to enable deprecation warnings])
AC_ARG_ENABLE(deprecations,
        [AC_HELP_STRING([--enable-deprecations],
        [enable deprecation warnings [default=no]])],,
        [enable_deprecations=no])
if test "x$enable_deprecations" = "xyes"; then
        AC_MSG_RESULT([yes])
        DISABLE_DEPRECATED_CFLAGS="\
                -DG_DISABLE_DEPRECATED  -DGDK_DISABLE_DEPRECATED \
                -DGTK_DISABLE_DEPRECATED -DGDK_PIXBUF_DISABLE_DEPRECATED \
                -DGCONF_DISABLE_DEPRECATED -DGNOME_DISABLE_DEPRECATED"
        AC_SUBST(DISABLE_DEPRECATED_CFLAGS)
else
        AC_MSG_RESULT([no])
fi

# We require Scroll Keeper 0.3.5 or above for the help
SCROLLKEEPER_BUILD_REQUIRED=0.3.5
AC_SUBST(SCROLLKEEPER_BUILD_REQUIRED)


# Finally, generate the files
AC_OUTPUT([
sound-juicer.spec
Makefile
help/Makefile
help/sound-juicer/Makefile
data/Makefile
po/Makefile.in
libjuicer/Makefile
src/Makefile
tests/Makefile
])
